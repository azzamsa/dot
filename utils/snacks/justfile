#!/usr/bin/env -S just --justfile

set dotenv-load := true

alias d := dev
alias r := run
alias f := fmt
alias l := lint
alias t := test
alias c := comply
alias k := check
alias p := deploy

[doc('List available commands')]
_default:
    just --list --unsorted

[confirm('⚠️ This command will alter your system. Run recipe `setup`?')]
[doc('Setup the repository')]
setup:
    cargo binstall 'cargo-edit cargo-outdated dprint git-cliff bacon typos-cli'

[doc('Tasks to make the code-base comply with the rules. Mostly used in git hooks')]
comply: fmt lint test

[doc('Check if the repository comply with the rules and ready to be pushed')]
check: fmt-check lint test

[doc('Publish the binaries')]
deploy: check
    cargo test

[doc('Develop the app')]
dev:
    bacon

[doc('Run the app')]
run:
    cargo run

[doc('Format the codebase.')]
fmt:
    cargo fmt --all

[doc('Check is the codebase properly formatted')]
fmt-check:
    cargo fmt --all -- --check

[doc('Lint the codebase')]
lint:
    cargo clippy --all-targets --all-features
    typos

[doc('Test the codebase')]
test:
    cargo test

[doc('Check dependencies health. Pass `--write` to upgrade dependencies')]
up arg="":
    if [ "{{ arg }}" = "--write" ]; then \
        cargo upgrade --incompatible --recursive --verbose && \
        cargo update && \
        dprint config update; \
    else \
        cargo outdated --root-deps-only; \
    fi

[doc('Dependency analysis')]
meta:
    cargo +nightly udeps
    cargo audit
